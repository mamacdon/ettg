# ###########################################################################
# BNF-ish format
#
# `<start>`  the special starting symbol
# `|`        separates choices of a production rule
# `;;`       terminates a rule
# `#`        begins a comment (continues to end of line)
#
# Sequential whitespaces are collapsed. Trailing whitespaces are ignored.
#
# ###########################################################################

# Crude way of decreasing sequel likelihood to 1/5
<start> := <nosequel> | <nosequel> | <nosequel> | <nosequel> | <sequel> ;;

# Use title, sequel, subtitle tags in HTML markup
<nosequel> := [title]<title>[/title] ;;

<title> := <classic_title> | <classic_title> | <classic_title> | <extended_title> ;;

<classic_title> := <adjective> <noun>;;

<extended_title> := 
	  the <noun_s> and the <noun_s> # The heart and the desire
	| <verb_ppl> by <noun_pl>       # Violated by obsessions
	| <verb_ppl> to <verb_inf>      # Stripped to kill
	| return to <noun_abstr_s>      # Return to malice
	| height of <noun_desire>       # Height of desire
	| point of <noun_abstr_s>       # Point of seduction
	| <noun_desire> for <noun_sexy> # Urge for passion
	| <noun_desire> to <verb_inf>   # Urge to murder
	| victim of <noun_pl>           #
	| body of <noun_pl>             #
	| the color of the <noun_s>     # The color of the embrace
	| trial of <noun_abstr_s>       # Trial of passion
	| the art of <noun_abstr_s>     # The art of malice
	| <a_an> <noun_desire> for <noun_abstr_s>    # A taste for fear
	| driven to <verb_inf>          # Driven to murder
	| the virgin <noun_s>           # The virgin heart
	| blood of <noun_pl>
	| <noun_sexy> <noun_symptom>    # Murder syndrome
	| <noun_pl> of <noun_sexy>      # Crimes of passion
	| hard to <verb_inf>            # Hard to die
	| <noun_abstr_s> behind bars    # Vice Behind Bars
	| the price of <noun_abstr_s>  
	| the perfect <noun_sexy>
	| in the <noun_throes> of <noun_abstr_s>
	| the shape of <noun_abstr_s>
	| beyond <noun_abstr_s>
	| the <noun_abstr_s> <noun_interior>
	| <verb_inf_transitive> me <time> # Kiss me again
	;;

# in the heat of

# TODO restore this production when .com comes back into vogue
# | <noun_s>.com                  # Sweat.com, Obsession.com

<sequel> := [title]<classic_title>[/title] [sequel]<num>[/sequel] [subtitle]<subtitle>[/subtitle] ;;

# subtitles have a larger chance of being extended than regular title
<subtitle> := <classic_title> | <classic_title> | <extended_title> ;;

<num> := 2 | 2 | 2 | 3 | 3 | 4 | 5 | 6 | 7 | 8 | 9;;

# ###########################################################################
# NOUNS
# ###########################################################################
<noun> := <noun_s> | <noun_pl>;;

<noun_s> := <noun_conc_s> | <noun_abstr_s> | <noun_abstr_s>;; # abstr longer so more likely

# Concrete singular noun
<noun_conc_s> := <noun_strumpet>
	| roulette | kiss | fear | sweat | embrace | dance | agenda | hunter
	| fire | Eden | weapon | orchid | beast | heart | flesh | silk | outlaw | killer
	;;

# Abstract singular noun
<noun_abstr_s> := <noun_sexy> | <noun_desire>
	| heat | malice | malevolence | attraction | suspicion | chemistry | fear | touch
	| knowledge | deception | vice | influence | behavior | love | beauty | jeopardy
	| response | justice | pursuit | discretion | bounty | fascination | hunger 
	| heat | evidence | predator | embrace | night | witness | aggression | rage
	| death | bond | risk | innocence | comfort | need | revenge | truth | instinct
	| paradise | lie | crime | intrigue | betrayal | thrill
	;;

<noun_sexy> := passion | obsession | temptation | exposure | murder | seduction;;

<noun_desire> := urge | passion | obsession | lust | desire | longing | yearning
	| appetite | taste | bloodlust ;;

<noun_pl> := illusions | requirements | instincts | desires | confessions | secrets
	| liaisons | suspicions | perversions | affairs | encounters | emotions | inhibitions
	| obsessions | misfortunes | lies | sins | crimes | motives | murders | awakenings
	| boundaries | temptations | dreams | favors | lessons | relations | interludes
	| games | revelations | eyes | bodies | curves | senses | passions | rhythms
	;;

<noun_symptom> := affliction | syndrome ;;

<noun_strumpet> := dancer | seducer | temptress | succubus ;;

<noun_throes> := heat | throes | midst ;;

<noun_interior> := inside | within ;;

# ###########################################################################
# Adjectives
# ###########################################################################
<adjective> := <adj_number>
	| dead | deadly | evil | malicious | naked | sinful | sensual | cruel | sexual
	| lustful | forbidden | sinister | tawdry | guilty | dangerous | body | fatal | bedroom | bare
	| wild | female | twisted | burning | hard | family | scandalous | stripped | night
	| dark | hot | haunting | hidden | basic | random | virtual | human | erotic
	| intimate | destructive | fleshly | carnal | animal | criminal | obsessive | nightly | blue
	| dangerous | lethal | predatory | wicked | deep | indecent | midnight | private
	| secret | tainted | final | the last | cold | illicit | fugitive | perfect | concealed
	| full | unnatural | sultry | inhuman | heavy | murderous | lewd | licentious | lascivious
	| physical | collateral | bitter | mortal | bodily | terminal | ultimate | primal | caged
	| shapely | sheer | irresistible | killer | deviant | exploited
	;;

<adj_number> := double | triple;;

# ###########################################################################
# VERBS
# ###########################################################################
<verb_inf> := <verb_inf_intransitive> | <verb_inf_transitive> ;;
<verb_inf_intransitive> := die ;;
<verb_inf_transitive> := kill | secude | murder | burn | conceal | strip | use | thrill ;;

<verb_ppl> := violated | seduced | possessed | murdered | tainted | concealed | stripped | dressed ;;

# ###########################################################################
# Article
# ###########################################################################
<a_an> := __a_an__ ;;

<time> := again | tomorrow | never ;;
